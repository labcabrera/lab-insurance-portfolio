buildscript {
  ext {
    springBootVersion         = '2.0.3.RELEASE'
    springCloudVersion        = 'Finchley.RELEASE'
    springfoxSwaggerVersion   = '2.8.0'
    palantirPluginVersion     = '0.20.1'
  }
  repositories {
    mavenCentral()
    //maven { url "http://nexus:8080/nexus/content/groups/public/" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:${palantirPluginVersion}")
  }
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  group = 'org.lab.insurance.portfolio'
  version = version
  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
    //maven { url "http://nexus:8080/nexus/content/groups/public/" }
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }
}

project(":lab-insurance-portfolio-common") {

  dependencies {
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv"
    compile "org.projectlombok:lombok"
    testCompile "org.springframework.boot:spring-boot-starter-test"
  }

  jar {
    enabled = true
  }
  
  bootJar {
    classifier = 'boot'
    // workaround to create a simple java library instead of a spring boot jar
    mainClassName = 'org.lab.insurance.portfolio.core.Dummy'
  }
}

project(":lab-insurance-portfolio-core") {

  apply plugin: 'com.palantir.docker'

  dependencies {
    compile project(":lab-insurance-portfolio-common")
    compile "org.springframework.integration:spring-integration-amqp"
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
  }
  
  docker {
    name "labcabrera/${jar.baseName}"
    tags 'latest'
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
  }
}

project(":lab-insurance-portfolio-api") {

  apply plugin: 'com.palantir.docker'

  dependencies {
    compile project(":lab-insurance-portfolio-core")
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
  }
  
  docker {
    name "labcabrera/${jar.baseName}"
    tags 'latest'
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
  }
}
